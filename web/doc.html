<sw-page navbar-doc="active" title="Documentation">


<p>
Read the <doc href="/refdoc/Ocf.html">reference documentation</doc>.
</p>
<p>
Below is a guided tour of the library through examples.
</p>
<prepare-toc>
<toc/>
<section id="options" title="Defining options">
<p>
Let's create the <ml>host</ml> and <ml>port</ml> options
with default values <ml>"localhost"</ml> and <ml>80</ml>:
</p>
<oc>
let host = Ocf.string ~doc: "Host name" "localhost";;
let port = Ocf.int ~doc: "Port number" 80;;
</oc>
<p>
The <ml>~doc</ml> parameter is used to give some description of the
option. It will be used later. Note that <ml>Ocf.string</ml> and
<ml>Ocf.int</ml> return typed options, here
with types <ml>string Ocf.conf_option</ml> and <ml>int Ocf.conf_option</ml>.
These functions are specialisations of the
<doc href="Ocf#VALoption"><ml>Ocf.option</ml></doc> function. Other such
functions are provided (see <doc href="Ocf#convenient">here</doc>).
</p>
<p>
Option values are modified in-place; the current value
is retrieved using
<doc href="refdoc/Ocf#VALget"><ml>Ocf.get</ml></doc>:
</p>
<oc>Ocf.get host;;</oc>
<p>
Now we create a group <ml>server_options</ml>. A group is a set of
groups and options, each having a path (a list of strings) from
the root of the group. Each part of a path will be mapped to a
field in JSON objects.
</p>
<oc>let server_options =
  let g = Ocf.add Ocf.group ["host"] host in
  Ocf.add g ["port"] port ;;
</oc>
<p>Note that <ml>Ocf.group</ml> is not a function. Adding things
to the group returns a new one. Here we built a new group by
adding our option <ml>host</ml> to the path <ml>["host"]</ml>,
and <ml>port</ml> to the path <ml>["port"]</ml>.
</p>
</section>

<section id="readwrite" title="Input and output">
<p>We can already load values for our options from a JSON string
(the library also provide functions to
<doc href="Ocf#VALfrom_file">read</doc> from and
<doc href="Ocf#VALto_file">write</doc> to JSON files).
Let's define a JSON string and read it to get
values:
</p>
<oc>
let () = Ocf.from_string server_options
  {| { host: "foo.bar.net", port: 8080 } |};;
</oc>
<p>
Let's see the values of our options:
</p>
<oc>
Ocf.get host ;;
Ocf.get port ;;
</oc>
<p>
We can read another JSON string. Values defined in this JSON will
replace the previous values of our options, but of course only
when values are given.
</p>
<oc>
let () = Ocf.from_string server_options
  {| { host: "my.host.org", dummy: 42 } |} ;;
Ocf.get host (* changed *);;
Ocf.get port (* unchanged *) ;;
</oc>
<p>Note that the <ml>dummy</ml> field in the JSON object is ignored,
as it does not correspond to a defined option.
</p>
<p>
We can get a JSON representation of our group of options:
</p>
<oc>print_endline (Ocf.to_string server_options);;</oc>
<p>Since JSON format does not allow comments, Ocf uses
<ext-a href="http://fadefade.com/json-comments.html">this hack</ext-a>
to show documentation strings in the JSON object. An option having a
documentation string appears twice: first with the associated documentation
string, then associated with the option value of the correct type.
After reading the JSON object, only the second association is kept.
</p>
<p><doc href="Ocf#output">Output functions</doc> can be called
with <ml>~with_doc: false</ml> to prevent the output of the documentation
string:
</p>
<oc>print_endline (Ocf.to_string ~with_doc: false server_options);;</oc>
</section>

<section id="groups" title="Groups and paths">
<p>
Let's define three additional options, <ml>db_name</ml>, <ml>db_host</ml>
and <ml>db_port</ml>:
</p>
<oc>let db_name = Ocf.string "mydb";;
let db_host = Ocf.string "localhost";;
let db_port = Ocf.int 3306;;
</oc>
<p>
We can put all these options in a <ml>db_options</ml> group and see
the JSON output:
</p>
<oc>let db_options =
  let g = Ocf.add Ocf.group ["name"] db_name in
  let g = Ocf.add g ["host"] db_host in
  Ocf.add g ["port"] db_port;;
print_endline (Ocf.to_string db_options);;
</oc>
<p>
Now we can put <ml>server_options</ml> and <ml>db_options</ml> in
a new <ml>app_options</ml> group (with
<doc href="Ocf#VALadd_group"><ml>Ocf.add_group</ml></doc>),
each one being assigned a path from the root of this new group:
</p>
<oc>let app_options =
  let g = Ocf.add_group Ocf.group ["server"] server_options in
  Ocf.add_group g ["database"] db_options;;
print_endline (Ocf.to_string app_options);;
</oc>
<p>
The same result could have been obtained by adding all our options with full paths
to a single group, here <ml>app_options2</ml>:
</p>
<oc>
let app_options2 =
  let g = Ocf.add Ocf.group ["server" ; "host"] host in
  let g = Ocf.add g ["server" ; "port"] port in
  let g = Ocf.add g ["database" ; "name"] db_name in
  let g = Ocf.add g ["database" ; "host"] db_host in
  Ocf.add g ["database" ; "port"] db_port;;
print_endline (Ocf.to_string app_options2);;
</oc>
<p>
Using groups allows to define options in a modular way. Modules defining a
group of options do not need to known the full path of the options in the
configuration file.
</p>
</section>
<section id="wrappers" title="Wrappers">
<p>
A wrapper (<ml>'a Ocf.wrapper</ml>) is a pair of functions to convert
values of type <ml>'a</ml>
to JSON (<ml>'a -> Yojson.Safe.json</ml>) and from JSON
(<ml>?def: 'a -> Yojson.Safe.json -> 'a</ml>). The <ml>?def</ml> parameter
will be given the current value of the option the wrapper is associated to, if any.
Default wrappers do not use this parameter, but it is used in
<doc href="#records">record wrappers</doc>.
</p>
<p>An option is built using such a wrapper, like <ml>Ocf.Wrapper.string</ml> or
<ml>Ocf.Wrapper.int</ml> in the example above (used by <ml>Ocf.string</ml>
and <ml>Ocf.int</ml> convenient functions). Other wrappers and wrapper
combinators are available in the <doc href="refdoc/Ocf.Wrapper">Wrapper module</doc>.
</p>
<p>
For example, we can define a wrapper for lists of <ml>string * int</ml> pairs
and use this wrapper to create an option:
</p>
<oc>
let wrapper = Ocf.Wrapper.(list (pair string int));;
let pairs = Ocf.option wrapper [ ("hello", 1) ; ("world", 2)];;
</oc>
<p>
Let's see how this will be stored in JSON (and so the expected JSON to give
values for this option):
</p>
<oc>
let () =
  let group = Ocf.add Ocf.group ["dummy"] pairs in
  print_endline (Ocf.to_string group);;
</oc>
<p>
A wrapper can be created with function
<doc href="refdoc/Ocf.Wrapper#VALmake"><ml>Ocf.Wrapper.make</ml></doc>
by giving your own <ml>'a -> Yojson.Safe.json</ml> and
<ml>?def: 'a -> Yojson.Safe.json -> 'a</ml> functions.
</p>
</section>

<section id="records" title="Wrapping records with a ppx">
<p>
Since it is often useful to define record types to represent a configuration
and to be able to load and store such a configuration from and to a file,
one way want to create a wrapper from a record type.
</p>
<p>
This can be done by creating special functions to map values of such
record type to and from JSON and use these functions to make a wrapper but
this is repetitive. Fortunately,
Ocf comes with a ppx processor, in package <code>ocf.ppx</code> to automate
the generation of wrappers from record types.
</p>
<p>Here is an example. Let's change our previous example to define a server
configuration record type as:
</p>
<ocaml>type server = { host: string; port: int };;</ocaml>
<p>
To enable code generation, we add some annotations:
</p>
<oc>type server =
  { host: string [@ocf Ocf.Wrapper.string, "localhost"] [@ocf.label "hostname"] ;
    port: int [@ocf Ocf.Wrapper.int, 8080] ;
  } [@@ocf]
</oc>
<p>
To trigger the generation, the type definition must have a <ml>[@@ocf]></ml>
attribute.
Each field can have a <ml>[@ocf ]</ml> attribute (on the type)
with a pair of expressions. The first part of the pair is the wrapper,
the second part is the default value for the field.
</p>
<p>
Then, two values are generated: <ml>default_server</ml> is a value of
type <ml>server</ml> with the given default value for each field.
<ml>server_wrapper</ml> is the wrapper to load and store values of
type <ml>server</ml> from and to JSON. When a field is not present
in the JSON representation, the default value will be used, to
have a value for each field.
</p>
<p>
By default, a field in the JSON object has the same name as the corresponding field
of the record type. The <ml>[@ocf.label]</ml> attribute on the field type can
be used to specify another label to use in the JSON object, such as
<ml>"hostname"</ml> in the example above.
</p>
<p>
Let's define an option with our new wrapper and print its JSON representation:
</p>
<oc>
let server = Ocf.option server_wrapper default_server ;;
let group = Ocf.add Ocf.group ["server"] server ;;
let () = print_endline (Ocf.to_string group);;
</oc>
<p>
Here is an example of setting the value of our <ml>server</ml> option:
</p>
<oc>Ocf.get server ;;
Ocf.from_string group {| { "server" : { "hostname": "my.host.com" } } |} ;;
Ocf.get server ;;
</oc>
<p>
Note that giving only some fields of the record in a JSON object will
change only the corresponding fields of the record value (but a new record
value will be allocated, though):
</p>
<oc>
Ocf.from_string group {| {"server" : { "port": 9000 } } |};;
Ocf.get server;;
</oc>
</section>
<section id="parametrized" title="Parametrized record types">
<p>
One can define parametrized record types. In this case, the generated default
value and the wrapper for this type will have parameters:
</p>
<oc>
type ('a, 'b) entry = {
  key: 'a ; (* wrapper and default value will be parameters *)
  value: 'b list [@ocf Ocf.Wrapper.list, [] ] ;
           (* a wrapper will be expected as parameter forr 'b, but default value []
              is already provided *)
  value2: string option [@ocf.wrapper Ocf.Wrapper.option Ocf.Wrapper.string] ;
           (* a wrapper is provided, but no default value *)
  } [@@ocf];;
</oc>
</section>


</prepare-toc>
</sw-page>